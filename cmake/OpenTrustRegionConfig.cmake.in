# OpenTrustRegionConfig.cmake
# ---------------------------
#
# OpenTrustRegion cmake module.
# This module sets the following variables in your project:
#
# ::
#
#   OpenTrustRegion_FOUND - true if OpenTrustRegion and all required components found on the system
#   OpenTrustRegion_VERSION - OpenTrustRegion version in format Major.Minor.Release
#
#
# Available components: shared static int32 int64
#
# ::
#
#   shared - search for only shared library
#   static - search for only static library
#   int32 - search for only lp64-linked library
#   int64 - search for only ilp64-linked library
#
#
# Exported targets:
#
# ::
#
# If OpenTrustRegion is found, this module defines the following :prop_tgt:`IMPORTED`
# targets. Target is shared _or_ static, so, for both, use separate, not overlapping,
# installations. If no language components are requested, this module defines at least
# the opentrustregion target. ::
#
#   OpenTrustRegion::opentrustregion32 - the main OpenTrustRegion Fortran 32-bit integer library with header & defs attached.
#   OpenTrustRegion::opentrustregion64 - the main OpenTrustRegion Fortran 64-bit integer library with header & defs attached.
#   OpenTrustRegion::opentrustregion - alias to whichever of above is requested or present (32 preferred if both available).
#   OpenTrustRegion::OpenTrustRegion - alias to above
#
#
# Suggested usage:
#
# ::
#
#   find_package(OpenTrustRegion)
#   find_package(OpenTrustRegion 2.0.0 EXACT CONFIG REQUIRED COMPONENTS shared int32)
#
#
# The following variables can be set to guide the search for this package:
#
# ::
#
#   OpenTrustRegion_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_OpenTrustRegion - CMake variable, disables
#       find_package(OpenTrustRegion) when not REQUIRED, perhaps to force internal build

@PACKAGE_INIT@

set(otr OpenTrustRegion)

# set all valid components
set (_valid_components
     static
     shared
     int32
     int64
)

# check whether shared or static libraries exist
if(@BUILD_SHARED_LIBS@)  # BUILD_SHARED_LIBS
    set(${otr}_shared_FOUND 1)
else()
    set(${otr}_static_FOUND 1)
endif()

# check whether shared or static libraries were requested by the user
list(FIND ${otr}_FIND_COMPONENTS "shared" _seek_shared)
list(FIND ${otr}_FIND_COMPONENTS "static" _seek_static)

# check whether 32-bit or 64-bit integer libraries exist
if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-32.cmake")
    set(${otr}_int32_FOUND 1)
endif()
if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-64.cmake")
    set(${otr}_int64_FOUND 1)
endif()

# check whether 32-bit or 64-bit integer libraries were requested by the user
list(FIND ${otr}_FIND_COMPONENTS "int32" _seek_int32)
list(FIND ${otr}_FIND_COMPONENTS "int64" _seek_int64)

# check whether all requested components actually exist (checks the ${otr}_..._FOUND variables)
check_required_components(${otr})

# don't include targets if this file is being picked up by another project which has
# already built this as a subproject
if(NOT TARGET ${otr}::opentrustregion)

    # check if find_package requests 32-bit integer version
    if(_seek_int32 GREATER -1)
        set(_target "opentrustregion32")
        include("${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-32.cmake")
    # check if find_package requests 32-bit integer version
    elseif(_seek_int64 GREATER -1)
        set(_target "opentrustregion64")
        include("${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-64.cmake")
    # pick 32-bit integer version if it exists
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-32.cmake")
        set(_target "opentrustregion32")
        include("${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-32.cmake")
    # pick 64-bit integer version if it exists
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-64.cmake")
        set(_target "opentrustregion64")
        include("${CMAKE_CURRENT_LIST_DIR}/${otr}Targets-64.cmake")
    else()
        message("No targets found to import for ${otr} -- faulty installation")
    endif()

    # add aliases
    add_library(${otr}::opentrustregion ALIAS ${otr}::${_target})
    add_library(${otr}::${otr} ALIAS ${otr}::${_target})

    # print debug information if cmake version allows for introspection
    if (CMAKE_VERSION VERSION_GREATER 3.15)
        set(_target "opentrustregion")
        get_property(_loc TARGET ${otr}::${_target} PROPERTY LOCATION)
        get_property(_ill TARGET ${otr}::${_target} PROPERTY INTERFACE_LINK_LIBRARIES)
        get_property(_id TARGET  ${otr}::${_target} PROPERTY INCLUDE_DIRECTORIES)
        get_property(_iid TARGET ${otr}::${_target} PROPERTY INTERFACE_INCLUDE_DIRECTORIES)

        message(VERBOSE "found: ${${otr}_FOUND} ver: ${${otr}_VERSION}")
        message(VERBOSE "${otr}::${_target}")
        message(VERBOSE "loc ${_loc}")
        message(VERBOSE "ill ${_ill}")
        message(VERBOSE "id  ${_id}")
        message(VERBOSE "iid ${_iid}")
    endif()
endif()

